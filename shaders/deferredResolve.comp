#version 460
#extension GL_EXT_nonuniform_qualifier: enable

#include "pbr.glsl"
#include "environment.glsl"
#include "lights.glsl"
#include "scene.glsl"
#include "shadows.glsl"

layout (local_size_x = 16, local_size_y = 16) in;

// layout (std140, set = 0, binding = 0) uniform SceneData - scene.glsl

layout (set = 1, binding = 0) uniform sampler2D normalRenderTarget;
layout (set = 1, binding = 1) uniform sampler2D albedoRenderTarget;
layout (set = 1, binding = 2) uniform sampler2D pbrRenderTarget;
layout (set = 1, binding = 3) uniform sampler2D depthBuffer;
layout (set = 1, binding = 4) uniform sampler2D velocityBuffer; // velocity buffer is not actually used in this deferred resolve

layout (rgba16f, set = 1, binding = 5) uniform image2D outputImage;

layout (set = 2, binding = 0) uniform samplerCube environmentDiffuseAndSpecular;
layout (set = 2, binding = 1) uniform sampler2D lut;

layout (std140, set = 3, binding = 0) uniform ShadowCascadeData {
    CascadeSplit cascadeSplits[4];
    mat4 lightViewProj[4];
    DirectionalLight directionalLightData; // w is intensity
    float nearShadowPlane;
    float farShadowPlane;
    vec2 pad;
} shadowCascadeData;

layout (set = 4, binding = 0) uniform sampler2DShadow shadowMapSampler[];

//layout (std140, set = 5, binding = 0) uniform LightData {
//    PointLight pointLights[6];
//} lightData;
//
//layout (set = 6, binding = 0) uniform sampler2D shadowMaps[];


layout (push_constant) uniform PushConstants {
    int width;
    int height;
    int debug;
    int disableShadows;
    int pcfLevel;
} pushConstants;

vec3 reconstructPosition(ivec2 texCoord, float depth) {
    // Get normalized device coordinates
    vec2 texSize = vec2(pushConstants.width, pushConstants.height);
    vec2 ndc = (vec2(texCoord) + 0.5) / texSize * 2.0 - 1.0;

    // Reconstruct view-space position
    vec4 positionVS = sceneData.invProjection * vec4(ndc, depth, 1.0);
    positionVS /= positionVS.w;

    // Transform to world-space
    vec4 positionWS = sceneData.invView * positionVS;

    return positionWS.xyz;
}

float LinearizeDepth(float depth)
{
    float near = 10000.0;
    float far = 0.01;
    float z = depth * 2.0 - 1.0; // Convert to NDC
    return (2.0 * near * far) / (far + near - z * (far - near));
}

void main() {
    // All calculations are done in World Space unless explicitly declared otherwise
    ivec2 screenPos = ivec2(gl_GlobalInvocationID.xy);
    if (screenPos.x > pushConstants.width || screenPos.y > pushConstants.height) {
        return;
    }

    vec2 uv = (vec2(screenPos) + 0.5) / vec2(pushConstants.width, pushConstants.height);
    vec4 albedo = texture(albedoRenderTarget, uv);
    if (albedo.w != 1) {
        return;
    }
    float depth = texture(depthBuffer, uv).r;
    vec3 normal = texture(normalRenderTarget, uv).rgb;
    vec4 pbrData = texture(pbrRenderTarget, uv);
    vec3 position = reconstructPosition(screenPos, depth);

    float roughness = pbrData.g;
    float metallic = pbrData.r;
    float emissive = pbrData.b;


    vec3 N = normalize(normal);
    vec3 V = normalize(sceneData.cameraPos.xyz - position);

    vec3 L = normalize(-shadowCascadeData.directionalLightData.direction); // for point lights, light.pos - inPosition
    vec3 H = normalize(V + L);

    // SPECULAR
    float NDF = D_GGX(N, H, roughness);
    float G = G_SCHLICKGGX_SMITH(N, V, L, roughness);
    vec3 F0 = mix(vec3(0.04), albedo.xyz, metallic);
    vec3 F = F_SCHLICK(V, H, F0);

    vec3 numerator = NDF * G * F;
    float denominator = 4.0f * max(dot(N, V), 0.0f) * max(dot(N, L), 0.0f);
    vec3 specular = numerator / max(denominator, 0.001f);

    vec3 kS = F;
    vec3 kD = vec3(1.0f) - kS;
    kD *= 1.0f - metallic;

    // DIFFUSE
    float nDotL = max(dot(N, L), 0.0f);
    vec3 diffuse = Lambert(kD, albedo.xyz);

    // SHADOWS
    float shadowFactor = 1.0f;
    int cascadeLevel = getCascadeLevel(position + normal * 0.05f, sceneData.view, shadowCascadeData.cascadeSplits);
    if (cascadeLevel >= 0 && cascadeLevel <= 3) {
        float _tempShadowFactor;
        if (pushConstants.pcfLevel == 5) {
            _tempShadowFactor = getShadowFactorPCF5(position, shadowCascadeData.lightViewProj[cascadeLevel], shadowMapSampler[nonuniformEXT(cascadeLevel)], shadowCascadeData.farShadowPlane);
        } else {
            _tempShadowFactor = getShadowFactor(position, shadowCascadeData.lightViewProj[cascadeLevel], shadowMapSampler[nonuniformEXT(cascadeLevel)], shadowCascadeData.farShadowPlane);
        }

        if (nDotL <= 0.0) {
            //shadowFactor = 0;
            // shadow affects IBL greater as the angle approaches opposite
            shadowFactor = mix(_tempShadowFactor, 0.0f, -nDotL * 2) * metallic;
        } else {
            shadowFactor = _tempShadowFactor;
        }
    }

    if (pushConstants.disableShadows == 1) {
        shadowFactor = 1.0f;
    }

    float indirectAttenuation = mix(0.4, 1.0, shadowFactor);

    // IBL REFLECTIONS
    vec3 irradiance = DiffuseIrradiance(environmentDiffuseAndSpecular, N);
    vec3 reflectionDiffuse = irradiance * albedo.xyz * indirectAttenuation;

    vec3 reflectionSpecular = SpecularReflection(environmentDiffuseAndSpecular, lut, V, N, roughness, F) * indirectAttenuation;

    vec3 ambient = (kD * reflectionDiffuse + reflectionSpecular);


    vec3 finalColor = (diffuse + specular) * vec3(1.0f) * nDotL * shadowFactor;
    //vec3 finalColor = (diffuse + specular) * lightData.mainLight.color * nDotL;
    finalColor += ambient;

    imageStore(outputImage, screenPos, vec4(finalColor, albedo.w));

    if (pushConstants.debug == 1) {
        // Velocity
        vec2 velocity = texture(velocityBuffer, uv).rg * 10.0;
        vec4 debugColor = vec4(velocity.x, velocity.y, 0.0, 0.0);
        imageStore(outputImage, screenPos, debugColor);
        return;
    }

    if (pushConstants.debug == 2) {
        // Depth
        float linearDepth = LinearizeDepth(depth);
        float normalizedLinearDepth = linearDepth / 25.0;
        vec4 fragColor = vec4(vec3(normalizedLinearDepth), 1.0);
        imageStore(outputImage, screenPos, vec4(vec3(normalizedLinearDepth), 1.0f));
        return;
    }

    if (pushConstants.debug == 3) {
        float viewDepth = getViewSpaceDepth(position, sceneData.view);
        int cascadeLevel = selectCascadeLevel(viewDepth, shadowCascadeData.cascadeSplits);
        float value = viewDepth - shadowCascadeData.cascadeSplits[cascadeLevel].nearPlane;
        float diff = value / (shadowCascadeData.cascadeSplits[cascadeLevel].farPlane - shadowCascadeData.cascadeSplits[cascadeLevel].nearPlane);
        if (cascadeLevel == 0) {imageStore(outputImage, screenPos, vec4(vec3(diff, 0.0, 0.0), albedo.w));}
        if (cascadeLevel == 1) {imageStore(outputImage, screenPos, vec4(vec3(0.0, diff, 0.0), albedo.w));}
        if (cascadeLevel == 2) {imageStore(outputImage, screenPos, vec4(vec3(0.0, 0.0, diff), albedo.w));}
        if (cascadeLevel == 3) {imageStore(outputImage, screenPos, vec4(vec3(diff), albedo.w));}
        if (cascadeLevel == 4) {imageStore(outputImage, screenPos, vec4(1.0, 0.0, 1.0, 1.0));}
    }

    if (pushConstants.debug == 4) {
        imageStore(outputImage, screenPos, vec4(vec3(1 - shadowFactor), 1.0));
        imageStore(outputImage, screenPos, vec4(vec3(nDotL), 1.0));
    }

    imageStore(outputImage, screenPos, vec4(vec3(nDotL), 1.0));
}